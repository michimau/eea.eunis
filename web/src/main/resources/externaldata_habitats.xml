<?xml version="1.0" encoding="UTF-8" standalone="no"?>
<!DOCTYPE properties SYSTEM "http://java.sun.com/dtd/properties.dtd">
<properties>
  <comment>Linked data queries for habitat types</comment>
<!-- -->
<!-- The list of queries in this file. Separated by whitespace. -->
<!-- -->
  <entry key="queries">deliveries art17ranges art17distribution habitatdistribution
  natconcl2012 natarea2012 </entry>

<!-- -->
<!-- .endpoint, .title and .query are mandatory for all queries -->
<!-- .summary - use HTML format - i.e. escape & and < -->
<!-- -->



<!-- -->
<!-- Deliveries query -->
<!-- -->
  <entry key="deliveries.endpoint">http://cr.eionet.europa.eu/sparql</entry>
  <entry key="deliveries.title">Reportnet deliveries</entry>
  <entry key="deliveries.summary">Files delivered through Reportnet with relevance to this habitat type.</entry>

<!-- Links are defined as {queryId}.link.{labelColumn}={linkColumn} -->
<!-- If the label column in SPARQL query is bound to ?filetitle and link itself to ?file -->
<!-- then the property should be defined as: {queryId}.link.filetitle = file -->
<!-- This will produce <a href="file">filetitle</a> in result row -->
  <entry key="deliveries.link.filetitle">file</entry>
  <entry key="deliveries.link.envtitle">envelope</entry>
  <entry key="deliveries.column.envtitle">Envelope</entry>
  <entry key="deliveries.column.filetitle">File</entry>

<!-- Query -->
<!-- [IDENTIFIER] is replaced with actual habitat ID before query will be executed -->
  <entry key="deliveries.query"><![CDATA[
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX dct: <http://purl.org/dc/terms/> 
PREFIX rod: <http://rod.eionet.europa.eu/schema.rdf#> 

SELECT DISTINCT xsd:date(?released) AS ?released ?coverage ?envelope ?envtitle 
 IRI(bif:concat(?sourcefile,'/manage_document')) AS ?file ?filetitle 
WHERE { 
	GRAPH ?sourcefile { 
		_:reference ?pred <http://eunis.eea.europa.eu/habitats/[IDENTIFIER]> . 
		OPTIONAL { 
			_:reference rdfs:label ?label 
		} 
	} 
	?envelope rod:hasFile ?sourcefile; 
		rod:released ?released; 
		rod:locality _:locurl; 
		dct:title ?envtitle . 
		_:locurl rdfs:label ?coverage . 
	?sourcefile dct:title ?filetitle 
} ORDER BY DESC(?released)
]]></entry>
<!-- Returns a list of habitat codes where the above query will return something -->
  <entry key="deliveries.codes"><![CDATA[
PREFIX eunis: <http://eunis.eea.europa.eu/rdf/habitats-schema.rdf#>
PREFIX rod: <http://rod.eionet.europa.eu/schema.rdf#>

SELECT DISTINCT ?habitatcode
WHERE {
 ?reference <http://rdfdata.eionet.europa.eu/article17/ontology/forHabitat> ?habitaturl .
 ?habitaturl eunis:code ?habitatcode
}
]]></entry>



<!-- -->
<!-- .endpoint, .title and .query are mandatory for all queries -->
<!-- .summary - use HTML format - i.e. escape & and < -->
<!-- -->
  <entry key="art17distribution.endpoint">http://cr.eionet.europa.eu/sparql</entry>
  <entry key="art17distribution.title">Distributions for this habitat type reported under Article 17, Habitats Directive</entry>
<!-- .summary - use HTML format - i.e. escape & and < -->
  <entry key="art17distribution.summary"><![CDATA[Assessments on the conservation status of the habitat types 
   and species of Community interest have been carried out in EU 25 for the period 2001-2006, 
   compiled as part of the Habitats Directive - Article 17 reporting process. 
   The data summary sheet for habitat type conservation status provides an overview per biogeographical region.]]></entry>
  <entry key="art17distribution.column.pc_rsa">Percent</entry>
  <entry key="art17distribution.column.surefacearea">Surface area</entry>
  <entry key="art17distribution.column.region">Region</entry>
  <entry key="art17distribution.column.country">Country</entry>
  <entry key="art17distribution.column.range">Range reference</entry>
  <entry key="art17distribution.query"><![CDATA[
PREFIX art17: <http://eunis.eea.europa.eu/rdf/art17report2008/ontology.rdf#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?country ?region ?surefacearea ?pc_rsa ?conclusion ?trend ?favourable_area ?quality 
WHERE { 
  ?subj a ?rdfType ; 
        art17:forHabitatType <http://eunis.eea.europa.eu/habitats/[IDENTIFIER]>; 
        art17:forRegion _:regionUrl . 
  _:regionUrl rdfs:label ?region . 
OPTIONAL { ?subj  art17:forCountry _:countryUrl . 
           _:countryUrl rdfs:label ?country } 
OPTIONAL {?subj art17:coverage_surface_area ?surefacearea} 
OPTIONAL {?subj art17:percentage_coverage_surface_area ?pc_rsa} 
OPTIONAL {?subj art17:conclusion_area _:conclusion_areaUrl . 
         _:conclusion_areaUrl rdfs:label ?conclusion } 
OPTIONAL {?subj art17:coverage_trend ?trendUrl .
          ?trendUrl rdfs:label ?trend } 
OPTIONAL {?subj art17:complementary_favourable_area ?favourable_area} 
OPTIONAL {?subj art17:coverage_quality ?quality} 

FILTER(?rdfType IN (art17:Habitattypesummary, art17:HabitatTypeManualAssessment)) 
} ORDER BY ?country ?region
]]></entry>
  <entry key="art17distribution.codes"><![CDATA[
PREFIX art17: <http://eunis.eea.europa.eu/rdf/art17report2008/ontology.rdf#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX eunis: <http://eunis.eea.europa.eu/rdf/habitats-schema.rdf#>

SELECT DISTINCT ?habitatcode
WHERE { 
  ?subj a ?rdfType ; 
        art17:forHabitatType ?habitaturl .
  ?habitaturl eunis:code ?habitatcode
FILTER(?rdfType IN (art17:Habitattypesummary, art17:HabitatTypeManualAssessment)) 
}
]]></entry>


  <entry key="art17ranges.endpoint">http://cr.eionet.europa.eu/sparql</entry>
  <entry key="art17ranges.title">Ranges for this habitat type reported under Article 17, Habitats Directive</entry>
<!-- .summary - use HTML format - i.e. escape & and < -->
  <entry key="art17ranges.summary"><![CDATA[Assessments on the conservation status of the habitat types 
   and species of Community interest have been carried out in EU 25 for the period 2001-2006, 
   compiled as part of the Habitats Directive - Article 17 reporting process. 
   The data summary sheet for habitat type conservation status provides an overview per biogeographical region.]]></entry>
  <entry key="art17ranges.column.pc_rsa">Percent</entry>
  <entry key="art17ranges.column.surfacerange">Surface of range</entry>
  <entry key="art17ranges.column.region">Region</entry>
  <entry key="art17ranges.column.country">Country</entry>
  <entry key="art17ranges.column.range">Range reference</entry>
  <entry key="art17ranges.query"><![CDATA[
PREFIX art17: <http://eunis.eea.europa.eu/rdf/art17report2008/ontology.rdf#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?country ?region ?surfacerange ?pc_rsa ?conclusion ?trend 
  bif:concat(?complementary_favourable_range_q, STR(?complementary_favourable_range)) AS ?favourable_range 
 ?quality 
WHERE { 
  ?subj a ?rdfType ; 
        art17:forHabitatType <http://eunis.eea.europa.eu/habitats/[IDENTIFIER]>; 
        art17:forRegion _:regionUrl . 
  _:regionUrl rdfs:label ?region . 
OPTIONAL { ?subj  art17:forCountry _:countryUrl . 
           _:countryUrl rdfs:label ?country } 
OPTIONAL {?subj art17:range_surface_area ?surfacerange} 
OPTIONAL {?subj art17:percentage_range_surface_area ?pc_rsa} 
OPTIONAL {?subj art17:conclusion_range _:conclusion_rangeUrl . 
         _:conclusion_rangeUrl rdfs:label ?conclusion } 
OPTIONAL {?subj art17:range_trend ?trendUrl .
          ?trendUrl rdfs:label ?trend } 
OPTIONAL {?subj art17:complementary_favourable_range_q ?complementary_favourable_range_q} 
OPTIONAL {?subj art17:complementary_favourable_range ?complementary_favourable_range} 
OPTIONAL {?subj art17:range_quality ?quality} 
FILTER(?rdfType IN (art17:Habitattypesummary, art17:HabitatTypeManualAssessment)) 
} ORDER BY ?country ?region
]]></entry>
  <entry key="art17ranges.codes"><![CDATA[
PREFIX art17: <http://eunis.eea.europa.eu/rdf/art17report2008/ontology.rdf#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX eunis: <http://eunis.eea.europa.eu/rdf/habitats-schema.rdf#>

SELECT DISTINCT ?habitatcode
WHERE { 
  ?subj a ?rdfType ; 
        art17:forHabitatType ?habitaturl .
  ?habitaturl eunis:code ?habitatcode
FILTER(?rdfType IN (art17:Habitattypesummary, art17:HabitatTypeManualAssessment)) 
}
]]></entry>


  <entry key="natconcl2012.endpoint">http://cr.eionet.europa.eu/sparql</entry>
  <entry key="natconcl2012.title">Conclusions for this habitat type reported in 2013 under Article 17, Habitats Directive</entry>
  <entry key="natconcl2012.attribution"><![CDATA[
<div style="font-size:130%; background-color: #ff8080;border:1px solid gray;margin:1em 3em;padding:0.5em;text-align:center">Unvalidated data: country deliveries</div>
]]></entry>
<!-- .summary - use HTML format - i.e. escape & and < -->
  <entry key="natconcl2012.summary"><![CDATA[Assessments on the conservation status of the habitat types 
   and species of Community interest have been carried out in EU 25 for the period 2007-2012, 
   compiled as part of the Habitats Directive - Article 17 reporting process. 
   The data summary sheet for habitat type conservation status provides an overview per biogeographical region.
]]></entry>
  <entry key="natconcl2012.column.filetitle">Source</entry>
  <entry key="natconcl2012.link.filetitle">sourcefile</entry>
  <entry key="natconcl2012.column.country">Country</entry>
  <entry key="natconcl2012.column.region">Region</entry>
  <entry key="natconcl2012.column.conclusion_range">Range</entry>
  <entry key="natconcl2012.column.conclusion_structure">Structure</entry>
  <entry key="natconcl2012.column.conclusion_area">Area</entry>
  <entry key="natconcl2012.column.conclusion_future">Future</entry>
  <entry key="natconcl2012.column.conclusion_assessment">Overall</entry>
  <entry key="natconcl2012.column.trend">Trend</entry>
  <entry key="natconcl2012.query"><![CDATA[
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX dct: <http://purl.org/dc/terms/> 
PREFIX rod: <http://rod.eionet.europa.eu/schema.rdf#> 
PREFIX a: <http://rdfdata.eionet.europa.eu/article17/ontology/>

SELECT ?filetitle
   IRI(bif:concat('http://cdr.eionet.europa.eu/Converters/run_conversion?file=',
      SUBSTR(STR(?sourcefile),29),'&conv=350&source=remote#', ?habitatcode)) AS ?sourcefile
   ?country ?region
   ?conclusion_range ?conclusion_structure ?conclusion_area ?conclusion_future
   ?conclusion_assessment ?conclusion_assessment_trend AS ?trend
WHERE {
  GRAPH ?sourcefile {
    ?regionalrep a a:HabitatTypeRegionalReport ;
                 a:forHabitat <http://eunis.eea.europa.eu/habitats/[IDENTIFIER]>;
                 a:habitatcode ?habitatcode;
                 a:region ?regionUri .
  }
  OPTIONAL { ?regionalrep  a:conclusion_range _:conclusion_range .
    _:conclusion_range rdfs:label ?conclusion_range }
  OPTIONAL { ?regionalrep  a:conclusion_structure _:conclusion_structure .
    _:conclusion_structure rdfs:label ?conclusion_structure }
  OPTIONAL { ?regionalrep  a:conclusion_area _:conclusion_area .
    _:conclusion_area rdfs:label ?conclusion_area }
  OPTIONAL { ?regionalrep  a:conclusion_future _:conclusion_future .
    _:conclusion_future rdfs:label ?conclusion_future }
  OPTIONAL { ?regionalrep  a:conclusion_assessment _:conclusion_assessment .
    _:conclusion_assessment rdfs:label ?conclusion_assessment }
  OPTIONAL { ?regionalrep  a:conclusion_assessment_trend _:conclusion_assessment_trend .
    _:conclusion_assessment_trend rdfs:label ?conclusion_assessment_trend }
  ?envelope rod:hasFile ?sourcefile .
  ?envelope rod:released ?released . FILTER(?released > "2013-01-01"^^xsd:date)
  ?envelope rod:locality _:locurl .
  _:locurl rdfs:label ?country .
  ?envelope dct:title ?filetitle .

  ?regionUri rdfs:label ?region .

} ORDER BY ?country ?region
]]></entry>
  <entry key="natconcl2012.codes"><![CDATA[
PREFIX a: <http://rdfdata.eionet.europa.eu/article17/ontology/>
PREFIX rod: <http://rod.eionet.europa.eu/schema.rdf#> 
PREFIX eunis: <http://eunis.eea.europa.eu/rdf/habitats-schema.rdf#>

SELECT DISTINCT ?habitatcode
WHERE { 
  GRAPH ?sourcefile {
    ?subj a a:HabitatTypeRegionalReport;
          a:forHabitat ?habitaturl .
  }
  ?habitaturl eunis:code ?habitatcode .
  ?envelope rod:hasFile ?sourcefile .
  ?envelope rod:released ?released . FILTER(?released > "2013-01-01"^^xsd:date)
}
]]></entry>

  <entry key="natarea2012.endpoint">http://cr.eionet.europa.eu/sparql</entry>
  <entry key="natarea2012.title">Surface areas for this habitat type reported in 2013 under Article 17, Habitats Directive</entry>
  <entry key="natarea2012.attribution"><![CDATA[
<div style="font-size:130%; background-color: #ff8080;border:1px solid gray;margin:1em 3em;padding:0.5em;text-align:center">Unvalidated data: country deliveries</div>
]]></entry>
<!-- .summary - use HTML format - i.e. escape & and < -->
  <entry key="natarea2012.summary"><![CDATA[Assessments on the conservation status of the habitat types 
   and species of Community interest have been carried out in EU 27 for the period 2007-2012, 
   compiled as part of the Habitats Directive - Article 17 reporting process. 
   The data summary sheet for habitat type conservation status provides an overview per biogeographical region.
]]></entry>
  <entry key="natarea2012.column.filetitle">Source</entry>
  <entry key="natarea2012.link.filetitle">sourcefile</entry>
  <entry key="natarea2012.column.country">Country</entry>
  <entry key="natarea2012.column.region">Region</entry>
  <entry key="natarea2012.column.surface_area">Surface area</entry>
  <entry key="natarea2012.column.conclusion">Conclusion</entry>
  <entry key="natarea2012.column.trend">Trend</entry>
  <entry key="natarea2012.query"><![CDATA[
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX dct: <http://purl.org/dc/terms/> 
PREFIX rod: <http://rod.eionet.europa.eu/schema.rdf#> 
PREFIX a: <http://rdfdata.eionet.europa.eu/article17/ontology/>

SELECT
   ?filetitle
   IRI(bif:concat('http://cdr.eionet.europa.eu/Converters/run_conversion?file=',
       SUBSTR(STR(?sourcefile),29),'&conv=350&source=remote#', ?habitatcode)) AS ?sourcefile
   ?country ?region 
   ?surface_area ?conclusion ?trend
WHERE {
  GRAPH ?sourcefile {
    ?regionalrep a a:HabitatTypeRegionalReport ;
                 a:forHabitat <http://eunis.eea.europa.eu/habitats/[IDENTIFIER]>;
                 a:habitatcode ?habitatcode;
                 a:region ?regionUri .
    OPTIONAL { ?regionalrep a:coverage_surface_area ?surface_area }
    ?regionalrep a:coverage_trend _:coverage_trend;
                 a:conclusion_area _:conclusion_area
  }
  ?envelope rod:hasFile ?sourcefile .
  ?envelope rod:released ?released . FILTER(?released > "2013-01-01"^^xsd:date)
  ?envelope rod:locality _:locurl .
  _:locurl rdfs:label ?country .
  ?envelope dct:title ?filetitle .

  ?regionUri rdfs:label ?region .
  _:conclusion_area rdfs:label ?conclusion .
  _:coverage_trend rdfs:label ?trend .
} ORDER BY ?country ?region
]]></entry>
  <entry key="natarea2012.codes"><![CDATA[
PREFIX a: <http://rdfdata.eionet.europa.eu/article17/ontology/>
PREFIX rod: <http://rod.eionet.europa.eu/schema.rdf#> 
PREFIX eunis: <http://eunis.eea.europa.eu/rdf/habitats-schema.rdf#>

SELECT DISTINCT ?habitatcode
WHERE { 
  GRAPH ?sourcefile {
    ?subj a a:HabitatTypeRegionalReport;
          a:forHabitat ?habitaturl .
  }
  ?habitaturl eunis:code ?habitatcode .
  ?envelope rod:hasFile ?sourcefile .
  ?envelope rod:released ?released . FILTER(?released > "2013-01-01"^^xsd:date)
}
]]></entry>




  <entry key="habitatdistribution.endpoint">http://semantic.eea.europa.eu/sparql</entry>
  <entry key="habitatdistribution.title">Countries having this habitat type</entry>
  <entry key="habitatdistribution.summary">Data collected by the European Commission and ETC/BD in 2002 and 2005</entry>
  <entry key="habitatdistribution.query"><![CDATA[
PREFIX e: <http://eunis.eea.europa.eu/rdf/schema.rdf#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX dcterms: <http://purl.org/dc/terms/> 

SELECT ?country ?biogeoregion ?probability ?comment ?source ?date 
WHERE { 
 GRAPH <http://rdfdata.eionet.europa.eu/eunis/habitatdistribution.rdf.gz> {
 ?subject e:forHabitat <http://eunis.eea.europa.eu/habitats/[IDENTIFIER]>; 
          e:forCountry ?ucountry; 
          dcterms:source ?usource . 
 }
 OPTIONAL {?subject e:hasBioGeoRegion ?ubiogeoregion . 
         ?ubiogeoregion rdfs:label ?biogeoregion }  
 OPTIONAL {?subject e:probability ?probability }  
 OPTIONAL {?subject e:comment ?comment }  
 ?usource dcterms:title ?source; 
          dcterms:date ?date . 
 ?ucountry rdfs:label ?country 
} ORDER BY DESC(?date) ?country ?biogeoregion
]]></entry>
  <entry key="habitatdistribution.codes"><![CDATA[
PREFIX e: <http://eunis.eea.europa.eu/rdf/schema.rdf#> 
PREFIX eunis: <http://eunis.eea.europa.eu/rdf/habitats-schema.rdf#>

SELECT DISTINCT ?habitatcode
WHERE { 
  GRAPH <http://rdfdata.eionet.europa.eu/eunis/habitatdistribution.rdf.gz> {
    ?subject e:forHabitat ?habitaturl .
  }
  ?habitaturl eunis:code ?habitatcode
}
]]></entry>




<!-- -->
<!-- Syntaxa crosswalk query -->
<!-- -->
  <entry key="syntaxa.endpoint">http://semantic.eea.europa.eu/sparql</entry>
  <entry key="syntaxa.title">Crosswalk from EUNIS habitat types to phytosociological syntaxa</entry>
  <entry key="syntaxa.summary"><![CDATA[The European Vegetation Survey developed an overview of European vegetation units,
syntaxa, providing a standardising tool in classifying vegetation types across Europe.]]></entry>

  <entry key="syntaxa.column.alternatives">Other names</entry>
  <entry key="syntaxa.column.name">Syntaxon name</entry>

<!-- Query -->
<!-- [IDENTIFIER] is replaced with actual habitat ID before query will be executed -->
  <entry key="syntaxa.query"><![CDATA[
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX h: <http://eunis.eea.europa.eu/rdf/habitats-schema.rdf#>

SELECT ?name ?description sql:group_concat(?alternative,'; ') AS ?alternatives
WHERE {
 GRAPH <http://eunis.eea.europa.eu/rdf/habitats.rdf.gz> {
  <http://eunis.eea.europa.eu/habitats/[IDENTIFIER]> owl:sameAs ?EUNIS_code
  }
  _:rec skos:inScheme <http://www.synbiosys.alterra.nl/rdf/syntaxa/> .
  _:rec skos:closeMatch ?EUNIS_code .
  _:rec skos:prefLabel ?name .
  _:rec skos:definition ?description .
  _:rec skos:notation ?code
 OPTIONAL { _:rec skos:altLabel ?alternative }
} GROUP BY ?code ?name ?description ?EUNIS_code
]]></entry>
  <entry key="syntaxa.codes"><![CDATA[
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX h: <http://eunis.eea.europa.eu/rdf/habitats-schema.rdf#>

SELECT distinct ?code
WHERE {
  _:rec skos:inScheme <http://www.synbiosys.alterra.nl/rdf/syntaxa/> .
  _:rec skos:closeMatch ?EUNIS_code .

 GRAPH <http://eunis.eea.europa.eu/rdf/habitats.rdf.gz> {
  ?eunisurl owl:sameAs ?EUNIS_code;
            h:code ?code
  }
}
]]></entry>




</properties>
